#!/usr/bin/perl -w
#
# Munin plugin for glusterfs 3.2+
# This plugin can graph:
#  - Open and maximum fd count
#  - Read performance per brick
#  - Write performance per brick 
#
#  Intially written by Bernd Weber (https://github.com/berndmweber)
#
#---------------------
# Examples
# Create a symbolic link to glusterfs_<volume>_<open|readperf|writeperf>
#       ln -s /usr/share/munin/plugins/glusterfs_ /etc/munin/plugins/glusterfs_shared_open
#           graph open calls for all bricks of the 'shared' volume
#
#       ln -s /usr/share/munin/plugins/glusterfs_ /etc/munin/plugins/glusterfs_shared_writeperf
#           graph write performance for all bricks of the 'shared' volume
#
#---------------------
#
# Add the following to your /etc/munin/plugin-conf.d/munin-node:
#
#       [glusterfs_*]
#       user root                   # Mandatory
#       env.blocksize <blocksize>   # Optional
#       env.blockcount <blockcount> # Optional
#
# If you want to monitor a specific brick per volume you'll have to add an additional
# section for each brick per volume in your config file
#       [glusterfs_<volume>_*]
#       env.brick <brick_name>
#       env.share <share_export_path>
#
#---------------------
# Log
# Revision 0.2  04/27/2015
# -Added support if you want to monitor more than one volume on the same host
#
# Revision 0.1  01/31/2013
# -First version of the GlusterFS Munin plugin
#
#%# family=auto
#%# capabilities=autoconf

use strict;
use warnings;

my $Revision = 0.2;

my $DEBUG = 0;

my $Volume = ""; # Volume name
my $Brick = exists $ENV{'brick'} ? $ENV{'brick'} : ""; # Brick name
my $Share = exists $ENV{'share'} ? $ENV{'share'} : ""; # Share name
my $BlockSize = exists $ENV{'blocksize'} ? $ENV{'blocksize'} : "256"; # Block size for performance tests
my $BlockCount = exists $ENV{'blockcount'} ? $ENV{'blockcount'} : "1"; # Block count for performance tests
my $Type = "";  # Define what needs to be done

# Parse out Brick number desired from file name and remove whitespace
my $Parameters = `basename $0 | sed 's/^glusterfs_//g'` ;
chomp $Parameters;
my ($v, $t) = split( /_/, $Parameters );

if ( defined $b and $b ne "" ) {
  $Brick = $b;
}

if ( ! defined $v or $v eq "" ) {
  print "The volume name is required, use 'gluster volume info all' to list available volumes\n";
  exit 1;
} else {
  $Volume = $v;
}

if ( ! defined $t or $t eq "" ) {
  print "The type needs to be defined as one of the following: open|readperf|writeperf\n";
  exit 1;
} else {
  $Type = $t;
}

if ( $DEBUG == 1 ) {
  print "Volume: $Volume; Brick: $Brick; Type: $Type\n";
}

# Locate gluster command and remove whitespace
my $Command = `which gluster`;
chomp $Command;

if ( ! defined $Command or $Command eq "" ) {
  print "GlusterFS doesn't seem to be installed on your machine. Can't find 'gluster'.\n";
  exit 1;
}

# Use this to define future parameters to monitor
my %config = (
  open => {
    callparams => "open",
    label => 'Open FD count',
    title => "Volume $Volume: Open FD count",
    vlabel => 'FDs',
    graph_args => '--base 1000 -l 0',
    info_tag => "Open file connections",
    description => "Open file connections for Volume $Volume."
  },
  readperf => {
    callparams => "read-perf bs $BlockSize count $BlockCount",
    label => 'Read performance',
    title => "Volume $Volume: Read performance",
    vlabel => 'MBps/secs',
    graph_args => '--base 1000 -l 0',
    info_tag => "Read performance",
    description => "Read performance for Volume $Volume."
  },
  writeperf => {
    callparams => "write-perf bs $BlockSize count $BlockCount",
    label => 'Write performance',
    title => "Volume $Volume: Write performance",
    vlabel => 'MBps/secs',
    graph_args => '--base 1000 -l 0',
    info_tag => "Write performance",
    description => "Write performance for Volume $Volume."
  }
);

# Create Command parameters
my $CommandParams = " volume top $Volume ";
my $CommandBrickParams = "";
if ( defined $Brick and $Brick ne "" and defined $Share and $Share ne "") {
  $CommandBrickParams = " brick ${Brick}:${Share}";
}

$Command .= $CommandParams;
$Command .= $config{$Type}->{callparams};
$Command .= $CommandBrickParams;

if ( $DEBUG == 1 ) {
  print "Command: $Command\n";
}

# This will create a label name correctly
sub createLabelName {
  my $brickName = $_[0];
  $brickName =~ s/\./_/g; # Convert dots
  $brickName =~ s/:/_/g;  # Convert semicolons
  $brickName =~ s/\///g;  # Remove slashes
  return ($brickName);
}

# Read Output of gluster command
my @Output = qx($Command);

if ( $ARGV[0] and $ARGV[0] eq "autoconf" ) {
  my $foundBrick = 0;
  foreach my $Line (@Output) {
    $Line=~ s/\r//g;
    $Line=~ s/\n//g;

    #Find Brick name
    if ( $Line=~ /Brick: (.*)/i ) {
      $foundBrick = 1;
    }
  }
  if ( $foundBrick == 0 ) {
    print "no (No Bricks found!)\n";
    exit 1;
  }
  print "yes\n";
}

#Munin Config Options
if ( $ARGV[0] and $ARGV[0] eq "config" ) {
  print "graph_title $config{$Type}->{title}\n";
  print "graph_vlabel $config{$Type}->{vlabel}\n";
  print "graph_args $config{$Type}->{graph_args}\n";
  print "graph_scale yes\n";
  print "graph_category glusterfs\n";
  print "graph_info $config{$Type}->{description} <br />Generated by glusterfs_, Version $Revision<br />\n";

  foreach my $Line (@Output) {
    $Line=~ s/\r//g;
    $Line=~ s/\n//g;

    #Find Brick name
    if ( $Line=~ /Brick: (.*)/i ) {
      my ($brick) = ($1);
      my $bricklabel = createLabelName($brick);

      if ( $Type eq "open" ) {
        print "${bricklabel}_${Type}_current.label $brick $config{$Type}->{label} current\n";
        print "${bricklabel}_${Type}_current.info $brick Current $config{$Type}->{info_tag}\n";
        print "${bricklabel}_${Type}_max.label $brick $config{$Type}->{label} max\n";
        print "${bricklabel}_${Type}_max.info $brick Max $config{$Type}->{info_tag}\n";
      } else {
        print "${bricklabel}_${Type}_throughput.label $brick $config{$Type}->{label} throughput in MBps\n";
        print "${bricklabel}_${Type}_throughput.info $brick $config{$Type}->{info_tag} throughput in MBps\n";
        print "${bricklabel}_${Type}_time.label $brick $config{$Type}->{label} time in sec\n";
        print "${bricklabel}_${Type}_time.info $brick $config{$Type}->{info_tag} time in sec\n";
      }
    }
  }
  exit 0;
}

# Main function - returns values
# placeholder
my $bricklabel;

foreach my $Line (@Output) {
  $Line=~ s/\r//g;
  $Line=~ s/\n//g;

  #Find Brick name
  if ( $Line=~ /Brick: (.*)/i ) {
    my ($brick) = ($1);
    $bricklabel = createLabelName($brick);
  }

  if ( $Type eq "open" ) {
    if ( $Line=~ /Current\sopen\sfds:\s+(\d+),\sMax\sopen\sfds:\s+(\d+)/i ) {
      my ($current, $max) = ($1, $2);
      print "${bricklabel}_${Type}_current.value $current\n";
      print "${bricklabel}_${Type}_max.value $max\n";
    }
  } else {
    if ( $Line=~ /Throughput\s+(\d+).(\d+)\s+MBps\s+time\s+(\d+).(\d+)\s+secs/i ) {
      my ($tpi, $tpf, $ti, $tf) = ($1, $2, $3, $4);
      print "${bricklabel}_${Type}_throughput.value $tpi.$tpf\n";
      print "${bricklabel}_${Type}_time.value $ti.$tf\n";
    }
  }
}

exit 0;
